/*
 * Akka gRPC is not currently supported with Play through Gradle,
 * because it requires Play 2.7.x, and at the time of writing the
 * Gradle does not yet work with Play. See also:
 *
 * Track https://github.com/akka/akka-grpc/issues/342 for progress.
 */

buildscript {
  repositories {
    mavenLocal()
    gradlePluginPortal()
  }
  dependencies {
    // see https://plugins.gradle.org/plugin/com.lightbend.akka.grpc.gradle
    // for the currently latest version.
    classpath 'com.lightbend.akka.grpc:akka-grpc-gradle-plugin:0.1+125-f0fcbe6e'
    //classpath 'gradle.plugin.com.lightbend.akka.grpc:akka-grpc-gradle-plugin:0.1-RC1'
  }
}

plugins {
    id 'play'
    id 'idea'
    id 'java'
}

def playVersion = "2.7.0-M2"
def scalaVersion = System.getProperty("scala.binary.version", /* default = */ "2.12")

apply plugin: 'com.lightbend.akka.grpc.gradle'

// Enable Akka gRPC plugin, and generate Play modules:
akkaGrpc {
    language = "Java"
    generateClient = true
    generateServer = true
    generatePlay = true
}

model {
    components {
        play {
            platform play: playVersion, scala: scalaVersion, java: '1.8'
            injectedRoutesGenerator = true

            sources {
                extraJava(JavaSourceSet) {
                    source.srcDir "build/generated/source/proto/main/java"
                    source.srcDir "build/generated/source/proto/main/akkaGrpc"
                }
                twirlTemplates {
                    defaultImports = TwirlImports.JAVA
                }
            }
        }
    }
}

configurations {
    alpnagent
}

dependencies {
    compile "com.typesafe.play:play_$scalaVersion:$playVersion"

    play "com.typesafe.play:play-guice_$scalaVersion:$playVersion"
    play "com.typesafe.play:play-logback_$scalaVersion:$playVersion"
    play "com.typesafe.play:play-akka-http-server_$scalaVersion:$playVersion"
    play "com.lightbend.akka.grpc:akka-grpc-runtime_$scalaVersion:0.1+125-f0fcbe6e"
    play "io.grpc:grpc-stub:1.14.0"
    play "com.h2database:h2:1.4.197"

    playTest "org.assertj:assertj-core:3.6.2"
    playTest "org.awaitility:awaitility:2.0.0"

    // Configuration for modules that use Jetty ALPN agent
    alpnagent 'org.mortbay.jetty.alpn:jetty-alpn-agent:2.0.7'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        name "lightbend-maven-releases"
        url "https://repo.lightbend.com/lightbend/maven-release"
    }
    ivy {
        name "lightbend-ivy-release"
        url "https://repo.lightbend.com/lightbend/ivy-releases"
        layout "ivy"
    }
}

tasks.withType(PlayRun) {
    forkOptions.jvmArgs = ['-javaagent:' + configurations.alpnagent.asPath]
}
